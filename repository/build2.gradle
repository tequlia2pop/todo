apply from: 'databaseSetup.gradle'

apply plugin: 'groovy'

dependencies {
	compile project(':model')
	runtime "com.h2database:h2:$h2Version"
    testCompile "org.codehaus.groovy:groovy:$groovyVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.testng:testng:$testngVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
}

// 测试 task 执行顺序：
// ... -> testNG -> test -> aggregateTestReports
// -> startDatabase -> buildSchema -> integrationTest -> stopDatabase -> check -> ...

// 用于执行 TestNG 测试的增强 task
task testNG(type: Test) {
    useTestNG()
}

// 修改默认的 test task：排除集成测试，并指定单元测试报告的输出目录
test {
	exclude '**/*IntegTest.class'
	reports.html.destination = file("$buildDir/reports/unit")
}

// 用于汇集测试报告的增强 task
task aggregateTestReports(type: TestReport) {
    destinationDir = test.reports.html.destination
    reportOn test, testNG
}

// 用于集成测试的增强 task：只包含集成测试，并指定集成测试报告的输出目录
// startAndPrepareDatabase task 作为 integrationTest task 的依赖，integrationTest task 执行完后会执行 stopDatabase finalizer task
task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
    group = 'verification'
	include '**/*IntegTest.class'
    reports.html.destination = file("$buildDir/reports/integration")
	dependsOn startAndPrepareDatabase
    finalizedBy stopDatabase
}

// 使 testNG task 作为 test task 的依赖
test.dependsOn testNG

// 使 aggregateTestReports task 作为 check task 的依赖
check.dependsOn aggregateTestReports

// 使 integrationTest task 作为  check task 的依赖
check.dependsOn integrationTest