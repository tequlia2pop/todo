// 用于引导 H2 数据库测试环境

buildscript {
    repositories {
    	mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "com.h2database:h2:${h2Version}"
    }
}

ext.h2TcpPort = 9092
ext.h2StartupBlockMs = 2000

// 用于启动数据库的增强 task
task startDatabase(type: H2DatabaseStarter) {
	group = 'database'
    description = 'Starts database.'
    tcpPort = h2TcpPort
    blockMs = h2StartupBlockMs
}

// 用于关闭数据库的增强 task
task stopDatabase(type: JavaExec) {
    group = 'database'
    description = 'Stops database.'
    classpath = buildscript.configurations.classpath
    main = 'org.h2.tools.Server'
    args = ['-tcpShutdown', "tcp://localhost:${h2TcpPort}"]
}

// 用于构建数据库 schema 的增强 task
task buildSchema(type: JavaExec, dependsOn: startDatabase) {
    group = 'database'
    description = 'Builds database schema.'
    classpath = buildscript.configurations.classpath
    workingDir = projectDir
    main = 'org.h2.tools.RunScript'
    args = ['-url', 'jdbc:h2:~/todo', '-user', 'sa', '-script', 'create-todo.sql']
}

// buildSchema task 作为 startAndPrepareDatabase task 的依赖
// startAndPrepareDatabase task 应该作为测试测试 task 的依赖
task startAndPrepareDatabase(dependsOn: buildSchema)

// 自定义 Task 类，用于启动 H2 数据库
class H2DatabaseStarter extends DefaultTask {
    @Input
    Integer tcpPort

    @Input
    Integer blockMs

    @TaskAction
    void start() {
        new Thread(new H2Server(tcpPort)).start()
        Thread.sleep(blockMs)
    }

    private class H2Server implements Runnable {
        final Integer tcpPort

        H2Server(Integer tcpPort) {
            this.tcpPort = tcpPort
        }

        @Override
        void run() {
            org.h2.tools.Server.main('-tcp', '-tcpPort', tcpPort.toString())
        }
    }
}